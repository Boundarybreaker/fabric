import net.fabricmc.loom.task.RemapJar

buildscript {
	repositories {
		jcenter()
		gradlePluginPortal()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.0-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "fabric"

def baseVersion = "0.2.3"
def mcVersion = "19w11b"

def ENV = System.getenv()
version = baseVersion + "." + (ENV.BUILD_NUMBER ?: "local")

minecraft {
	refmapName = "net.fabricmc.fabric.refmap.json"
}

repositories {
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion.3"
	modCompile "net.fabricmc:fabric-loader:0.3.7.109"
	shadow "blue.endless:jankson:1.1.1"
	implementation "blue.endless:jankson:1.1.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

shadowJar {
	//Relocate the Jankson into the Fabric jar.
	//This may be unnecessary, but it allows others to write their own Jankson config manager if they so desire.
	relocate 'blue.endless.jankson', 'net.fabricmc.fabric.jankson'

	classifier = null
	configurations = [project.configurations.shadow]
}

jar {
	classifier = "slim"
}

task remapShadowJar(type: RemapJar) {
	jar = shadowJar.archivePath
}
tasks.remapShadowJar.dependsOn tasks.shadowJar
tasks.assemble.dependsOn tasks.remapShadowJar

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '306612'
		changelog = 'A changelog can be found at https://github.com/FabricMC/fabric/commits/master'
		releaseType = 'alpha'
		addGameVersion '1.14-Snapshot'
		mainArtifact(jar) {
			displayName = "[$mcVersion] Fabric API $baseVersion build $ENV.BUILD_NUMBER"
		}
	}
	options{
		forgeGradleIntegration = false
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'
